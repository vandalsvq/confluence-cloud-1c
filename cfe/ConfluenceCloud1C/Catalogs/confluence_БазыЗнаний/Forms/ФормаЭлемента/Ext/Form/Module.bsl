
#Область УправлениеФормой

&НаСервере
Процедура ИнициализацияФормы()
	
	ЗаполнитьНастройкиИзХранилищаНаСервере();
	
	ЭтотОбъект.РазделПоУмолчанию			= НЕ ПустаяСтрока(Объект.КлючПространства);
	ЭтотОбъект.ОграничитьДоступПоСтраницам	= (ЭтотОбъект.РазделПоУмолчанию И Объект.ДоступныеСтраницы.Количество() > 0);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, ИмяРеквизита)

	Если НЕ Обработано.Найти(ИмяРеквизита) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Обработано.Добавить(ИмяРеквизита);

	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;
	
	РазделУстановлен = (Форма.РазделПоУмолчанию И ЗначениеЗаполнено(Объект.КлючПространства));

	#Область Наборы
	
	Если ИмяРеквизита = "РеквизитыРазделПоУмолчанию" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "КлючПространства");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "СкрытьПолеВводаРаздела");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "СтраницыПоУмолчанию");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "СписокСтраницПредставление");
	КонецЕсли;
	
	Если ИмяРеквизита = "РеквизитыДоступа" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ПользовательЛогин");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ПользовательТокен");
	КонецЕсли;
	
	#КонецОбласти
	
	#Область Элементы
	
	Если ИмяРеквизита = "КлючПространства" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"КлючПространства", "Видимость", Форма.РазделПоУмолчанию);
	КонецЕсли;
	
	Если ИмяРеквизита = "СкрытьПолеВводаРаздела" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СкрытьПолеВводаРаздела", "Видимость", РазделУстановлен);
	КонецЕсли;
	
	Если ИмяРеквизита = "СтраницыПоУмолчанию" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СтраницыПоУмолчанию", "ТолькоПросмотр", НЕ РазделУстановлен);
	КонецЕсли;
	
	Если ИмяРеквизита = "СписокСтраницПредставление" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СписокСтраницПредставление", "Видимость", РазделУстановлен И Форма.ОграничитьДоступПоСтраницам);
	КонецЕсли;
	
	Если ИмяРеквизита = "ПользовательЛогин" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ПользовательЛогин", "Видимость", НЕ Объект.ИспользоватьАнонимныйДоступ);
	КонецЕсли;
	
	Если ИмяРеквизита = "ПользовательТокен" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ПользовательТокен", "Видимость", НЕ Объект.ИспользоватьАнонимныйДоступ);
	КонецЕсли;
	
	#КонецОбласти 
	
	#Область ТабЧасть_Имя
	
	//Если ИмяРеквизита = "ИмяТабличнойЧастиОтветственный" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
	//	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	//		"ИмяТабличнойЧастиОтветственный", "ТолькоПросмотр", ЗначениеЗаполнено(Объект.Ответственный));
	//КонецЕсли;

	#КонецОбласти
	
	#Область Команды
	
	//Если ИмяРеквизита = "КомандаЗаполнить" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
	//	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	//		"ТаблицаФормыЗаполнить", "Видимость", НЕ Объект.Проведен);
	//КонецЕсли;

	#КонецОбласти 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформление(Форма, ИменаРеквизитов = "")

	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		Если ПустаяСтрока(ИменаРеквизитов) Тогда
			МассивИмен = Новый Массив;
			МассивИмен.Добавить("");
		Иначе
			МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаРеквизитов, ",");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Массив") Тогда
		МассивИмен = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ИменаРеквизитов);
	Иначе
		Возврат;
	КонецЕсли;
 
	//Форма.ТолькоПросмотр = (Форма.СостоянияЗаблокировано.Найти(Форма.СведенияОЗаявкеСостояние) <> Неопределено);

	Обработано = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИмен Цикл
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, СокрЛП(ИмяРеквизита));
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресСервераПриИзменении(Элемент)
	
	ЗаполнитьСписокПространствНаКлиенте();
	
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыРазделПоУмолчанию");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАнонимныйДоступПриИзменении(Элемент)
	
	Если Объект.ИспользоватьАнонимныйДоступ Тогда
		Объект.ПользовательЛогин = "";
		ЭтотОбъект.ПользовательТокен = "";
	КонецЕсли;
	
	ЗаполнитьСписокПространствНаКлиенте();
	
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыДоступа");
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательЛогинПриИзменении(Элемент)
	
	ЗаполнитьСписокПространствНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПользовательТокенПриИзменении(Элемент)
	
	ЗаполнитьСписокПространствНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура РазделПоУмолчаниюПриИзменении(Элемент)
	
	Если НЕ ЭтотОбъект.РазделПоУмолчанию Тогда
		Объект.КлючПространства			= "";
		Объект.СкрытьПолеВводаРаздела	= Ложь;
		Объект.ДоступныеСтраницы.Очистить();
		
		ЭтотОбъект.ОграничитьДоступПоСтраницам = Ложь;
		ЭтотОбъект.СписокСтраницПредставление = "";
	КонецЕсли;
	
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыРазделПоУмолчанию");
	
КонецПроцедуры

&НаКлиенте
Процедура КлючПространстваПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.КлючПространства) Тогда
		Объект.СкрытьПолеВводаРаздела = Ложь;
		Объект.ДоступныеСтраницы.Очистить();
		
		ЭтотОбъект.ОграничитьДоступПоСтраницам = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокСтраницНаКлиенте();
	ОбновитьСписокСтраницПредставлениеНаКлиенте();
	
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыРазделПоУмолчанию");
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьДоступПоСтраницамПриИзменении(Элемент)
	
	Если НЕ ЭтотОбъект.ОграничитьДоступПоСтраницам Тогда
		Объект.ДоступныеСтраницы.Очистить();
		
		ЭтотОбъект.СписокСтраницПредставление = "";
	КонецЕсли;
	
	УстановитьУсловноеОформление(ЭтотОбъект, "СписокСтраницПредставление");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСтраницПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПометкаДоступныхСтраницЗавершение", ЭтотОбъект);
	
	ЭтотОбъект.СписокСтраниц.ПоказатьОтметкуЭлементов(ОписаниеОповещения, НСтр("ru='Доступные страницы'"));
КонецПроцедуры

&НаКлиенте
Процедура СписокСтраницПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	Объект.ДоступныеСтраницы.Очистить();
	
	ЭтотОбъект.СписокСтраницПредставление = "";
	ЭтотОбъект.Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		ИнициализацияФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСписокПространствНаКлиенте();
	ЗаполнитьСписокСтраницНаКлиенте();
	ОбновитьСписокСтраницПредставлениеНаКлиенте(Ложь);
	
	УстановитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ИнициализацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность", ЭтотОбъект.Модифицированность);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("Модифицированность") И ТекущийОбъект.ДополнительныеСвойства.Модифицированность Тогда
		ЗаписатьТокенПользователяВХранилищеНаСервере(ТекущийОбъект.Ссылка, Объект.ИспользоватьАнонимныйДоступ, ЭтотОбъект.ПользовательТокен);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	УдаляемыеРеквизиты = Новый Массив;
	
	Если Объект.ИспользоватьАнонимныйДоступ Тогда
		УдаляемыеРеквизиты.Добавить("ПользовательТокен");
	КонецЕсли;
	
	Если ЭтотОбъект.РазделПоУмолчанию Тогда
		ПроверяемыеРеквизиты.Добавить("Объект.КлючПространства");
	КонецЕсли;
	
	Если ЭтотОбъект.ОграничитьДоступПоСтраницам Тогда
		ПроверяемыеРеквизиты.Добавить("Объект.ДоступныеСтраницы");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, УдаляемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ПометкаДоступныхСтраницЗавершение(Результат, ДопПараметры) Экспорт	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.СписокСтраниц = Результат;
	ЭтотОбъект.Модифицированность = Истина;
	
	ОбновитьСписокСтраницПредставлениеНаКлиенте(Истина);
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура ЗаполнитьСписокПространствНаКлиенте()
	Если ПустаяСтрока(Объект.АдресСервера) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПодключения = ПолучитьНастройкиПодключения(ЭтотОбъект);
	Если НастройкиПодключения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = confluence_ApiClientServer.get_all_spaces_params(0, 0); 
	
	РезультатЗапроса = confluence_ApiClientServer.get_all_spaces(НастройкиПодключения, ПараметрыЗапроса); 
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.КлючПространства.СписокВыбора.Количество() > 0 Тогда
		Элементы.КлючПространства.СписокВыбора.Очистить();
	КонецЕсли;
	
	Для Каждого СтруктураРаздела Из РезультатЗапроса.Значения Цикл
		Элементы.КлючПространства.СписокВыбора.Добавить(СтруктураРаздела.Ключ, СтруктураРаздела.Наименование);
	КонецЦикла;
	
	Если Элементы.КлючПространства.СписокВыбора.НайтиПоЗначению(Объект.КлючПространства) = Неопределено Тогда
		Объект.КлючПространства = "";
	Иначе
		Объект.КлючПространства = Объект.КлючПространства;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ЗаполнитьСписокСтраницНаКлиенте()
	ЭтотОбъект.СписокСтраниц.Очистить();
	
	Если ПустаяСтрока(Объект.АдресСервера) Тогда
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.КлючПространства) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПодключения = ПолучитьНастройкиПодключения(ЭтотОбъект);
	Если НастройкиПодключения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДомашняяСтраница = confluence_ApiClientServer.get_home_page_of_space(НастройкиПодключения, Объект.КлючПространства); 
	Если confluence_ApiClientServer.check_error(ДомашняяСтраница, Неопределено) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатЗапроса = confluence_ApiClientServer.get_child_pages(НастройкиПодключения, ДомашняяСтраница.Идентификатор); 
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	МассивПометок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.ДоступныеСтраницы Цикл
		МассивПометок.Добавить(СтрокаТаблицы.Идентификатор);
	КонецЦикла;
	
	Для Каждого СтруктураСтраницы Из РезультатЗапроса.Значения Цикл
		ЭтотОбъект.СписокСтраниц.Добавить(СтруктураСтраницы.Идентификатор,
			СтруктураСтраницы.Заголовок,
			(МассивПометок.Найти(СтруктураСтраницы.Идентификатор) <> Неопределено));
	КонецЦикла;
КонецПроцедуры

&НаКлиенте 
Процедура ОбновитьСписокСтраницПредставлениеНаКлиенте(знач ОбновитьДоступныеСтраницы = Ложь)
	
	МассивПометок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.ДоступныеСтраницы Цикл
		МассивПометок.Добавить(СтрокаТаблицы.Идентификатор);
	КонецЦикла;
	
	Если ОбновитьДоступныеСтраницы = Истина Тогда
		Объект.ДоступныеСтраницы.Очистить();
	КонецЕсли;
	
	ЭтотОбъект.СписокСтраницПредставление = "";
	
	Для Каждого ЭлементСписка Из ЭтотОбъект.СписокСтраниц Цикл
		Если НЕ ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		Если ОбновитьДоступныеСтраницы Тогда
			Объект.ДоступныеСтраницы.Добавить().Идентификатор = ЭлементСписка.Значение;
		КонецЕсли;
		
		ЭтотОбъект.СписокСтраницПредставление = ЭтотОбъект.СписокСтраницПредставление
			+ ?(ПустаяСтрока(ЭтотОбъект.СписокСтраницПредставление), "", "; ")
			+ ЭлементСписка.Представление;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Функция ПолучитьНастройкиПодключения(Форма)
	Если Форма.Объект.ИспользоватьАнонимныйДоступ Тогда
		Возврат confluence_ApiClientServer.get_connection_settings(Форма.Объект.АдресСервера);
	ИначеЕсли ЗначениеЗаполнено(Форма.Объект.ПользовательЛогин) Тогда // И ЗначениеЗаполнено(Форма.ПользовательТокен)
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(Форма.ПользовательТокен) Тогда
			ТокенПользователя = ПолучитьТокенПользователяИзХранилищаНаСервере(Форма.Объект.Ссылка);
		Иначе 
			ТокенПользователя = Форма.ПользовательТокен;
		КонецЕсли;
		
		Возврат confluence_ApiClientServer.get_connection_settings(Форма.Объект.АдресСервера,
			Форма.Объект.ПользовательЛогин,
			ТокенПользователя);
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста 
Процедура ЗаписатьТокенПользователяВХранилищеНаСервере(знач БазаЗнанийСсылка, знач ИспользоватьАнонимныйДоступ, знач ТокенПользователя)
	Справочники.confluence_БазыЗнаний.ЗаписатьТокенПользователяВХранилище(БазаЗнанийСсылка, ИспользоватьАнонимныйДоступ, ТокенПользователя);
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьТокенПользователяИзХранилищаНаСервере(знач БазаЗнанийСсылка)
	Возврат Справочники.confluence_БазыЗнаний.ПолучитьТокенПользователяИзХранилища(БазаЗнанийСсылка);
КонецФункции

&НаСервере 
Процедура ЗаполнитьНастройкиИзХранилищаНаСервере()
	ТокенПользователя = ПолучитьТокенПользователяИзХранилищаНаСервере(Объект.Ссылка);
	Если ЗначениеЗаполнено(ТокенПользователя) Тогда
		ЭтотОбъект.ПользовательТокен = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
