
#Область ПрограммныйИнтерфейс_Общий

// Возвращает публичный адрес для скачивания вложения (без необходимости авторизации)
//
// Параметры:
//	Настройки		- Структура - см. get_connection_settings
//	СтраницаИД	- Строка - идентификатор страницы
//	ВложениеИД	- Строка - идентификатор вложения
//
// Возвращаемое значение:
//   Строка
//
Асинх Функция get_attachment_publiclink(знач Настройки, знач СтраницаИД, знач ВложениеИД) Экспорт
	ТекстЗапроса = СтрШаблон("/wiki/rest/api/content/%1/child/attachment/%2/download",
		СтраницаИД,
		ВложениеИД);
		
	Возврат Ждать get_data_publiclink(Настройки, ТекстЗапроса);
КонецФункции

// Вычисляет публичную ссылку для скачивания файла без необходимости авторизации
//
// Параметры:
//	Настройки		- Структура - см. get_connection_settings
//	ТекстЗапроса	- Строка - текст запроса 
//	Вариант			- Число - 
//		0 - запрос к /wiki/download/...
//		1 - получение данных файла PDF
//
// Возвращаемое значение:
//   Строка
//
Асинх Функция get_data_publiclink(знач Настройки, знач ТекстЗапроса, знач Вариант = 0) Экспорт
	#Область ЗапросHTTP
	ЗаголовкиHTTP	= confluence_ApiClientServer.ПолучитьЗаголовки(Настройки);
	ЗапросHTTP		= Новый HTTPЗапрос(ТекстЗапроса, ЗаголовкиHTTP);
	СоединениеHTTP	= confluence_ApiClientServer.ПолучитьСоединениеHTTP(Настройки);
	#КонецОбласти 
	
	ОтветHTTP = Ждать СоединениеHTTP.ПолучитьАсинх(ЗапросHTTP);
	Если Вариант = 0 И ОтветHTTP.КодСостояния <> 302 Тогда
		Возврат confluence_Objects.error(ОтветHTTP, НСтр("ru='Проверка наличия данных для скачивания (0).'"));
	ИначеЕсли Вариант = 1 И ОтветHTTP.КодСостояния <> 200 Тогда
		Возврат confluence_Objects.error(ОтветHTTP, НСтр("ru='Проверка наличия данных для скачивания (1).'"));
	КонецЕсли;
	
	Если Вариант = 0 Тогда
		АдресФайла = СвойствоСоответствия(ОтветHTTP.Заголовки, "location", "");
		Если НЕ ЗначениеЗаполнено(АдресФайла) Тогда
			АдресФайла = СвойствоСоответствия(ОтветHTTP.Заголовки, "Location", "");
		КонецЕсли;
	ИначеЕсли Вариант = 1 Тогда 
		АдресФайла = ОтветHTTP.ПолучитьТелоКакСтроку();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(АдресФайла) Тогда
		ВсеЗаголовки = Новый Массив;;
		Для Каждого КлючИЗначение Из ОтветHTTP.Заголовки Цикл
			ВсеЗаголовки.Добавить(КлючИЗначение.Ключ);
		КонецЦикла;
		
		ТекстОписания = СтрШаблон(НСтр("ru='Адрес переадресации не определен (все заголовки %1).'"), СтрСоединить(ВсеЗаголовки, "; "));
		Возврат confluence_Objects.error(ОтветHTTP, ТекстОписания);
	КонецЕсли;

	Возврат АдресФайла;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ОбщегоНазначения

Функция СвойствоСтруктуры(Структура, Ключ, ПоУмолчанию = Неопределено)
	Возврат confluence_CommonClientServer.СвойствоСтруктуры(Структура, Ключ, ПоУмолчанию);
КонецФункции

Функция СвойствоСоответствия(знач Соответствие, знач ПутьКСвойству, знач ПоУмолчанию = Неопределено, знач КакЧисло = Ложь)
	Возврат confluence_CommonClientServer.СвойствоСоответствия(Соответствие, ПутьКСвойству, ПоУмолчанию, КакЧисло);
КонецФункции

#КонецОбласти
