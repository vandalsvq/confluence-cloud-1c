#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЭкспортСтатьиPDFВФоне(знач Параметры, знач АдресХранилища = "", знач АдресДопХранилища = "") Экспорт 
	
	Результат = confluence_ApiClientServer.get_page_as_pdf(Параметры.Настройки, Параметры.СтраницаИД);
	Если НЕ ТипЗнч(Результат) = Тип("ДвоичныеДанные") Тогда
		confluence_ApiClientServer.check_error(Результат, Ложь);
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаписатьНастройкиВХранилище(знач Параметр = Неопределено, знач Настройки) Экспорт
	
	Если Метаданные.Справочники.Найти("confluence_БазыЗнаний") <> Неопределено Тогда
		Справочники.confluence_БазыЗнаний.ЗаписатьНастройки(Параметр, Настройки);
	Иначе 
		ЗаписатьНастройкиПереопределяемый(Настройки);
	КонецЕсли;
	
КонецПроцедуры
	
Функция ПолучитьНастройкиИзХранилища(знач Параметр = Неопределено) Экспорт
	
	Если Метаданные.Справочники.Найти("confluence_БазыЗнаний") <> Неопределено Тогда
		Возврат Справочники.confluence_БазыЗнаний.ПолучитьНастройки(Параметр);
	Иначе 
		Возврат ПолучитьНастройкиПереопределяемый(Параметр);
	КонецЕсли;
	
КонецФункции

Процедура НастройкиВОбъектФормы(знач ОбъектФормы, знач НастройкиФормы) Экспорт
	Настройки = СтруктураНастроек();
	ЗаполнитьЗначенияСвойств(Настройки, НастройкиФормы);
	
	ЗаполнитьЗначенияСвойств(ОбъектФормы, Настройки,, "ДоступныеСтраницы");
	
	ОбъектФормы.ДоступныеСтраницы.Очистить();
	
	Для Каждого СтруктураСтроки Из Настройки.ДоступныеСтраницы Цикл
		СтрокаТаблицы = ОбъектФормы.ДоступныеСтраницы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураСтроки);
	КонецЦикла;
КонецПроцедуры

Функция НастройкиИзОбъектаФормы(знач ОбъектФормы) Экспорт
	Настройки = СтруктураНастроек();
	ЗаполнитьЗначенияСвойств(Настройки, ОбъектФормы,, "ДоступныеСтраницы");
	
	МассивСтрок = ОбщегоНазначения.ТаблицаЗначенийВМассив(ОбъектФормы.ДоступныеСтраницы.Выгрузить());
	Настройки.ДоступныеСтраницы = МассивСтрок;
	
	Возврат Настройки;
КонецФункции

Функция СтруктураНастроек() Экспорт
	Структура = Новый Структура;
	Структура.Вставить("БазаЗнанийСсылка"			, Неопределено);
	Структура.Вставить("АдресСервера"				, "");
	Структура.Вставить("ПользовательЛогин"			, "");
	Структура.Вставить("ПользовательТокен"			, "");
	Структура.Вставить("ОткрыватьВнешниеСсылкиВ1С"	, Ложь);
	Структура.Вставить("КлючПространства"			, "");
	Структура.Вставить("СкрытьПолеВводаРаздела"		, Ложь);
	Структура.Вставить("ДоступныеСтраницы"			, Новый Массив);
	
	Возврат Структура;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Переопределяемые

// Выполняет запись настроек работы с базой знаний
//
// Параметры:
//	Настройки	- Структура - см. СтруктураНастроек
//
Процедура ЗаписатьНастройкиПереопределяемый(знач Настройки)
	//ХранилищеНастроек = Новый ХранилищеЗначения(Настройки);
	//// выполнение записи
	//
КонецПроцедуры

// Возвращает структуру настроек для доступа к базе знаний
//
// Возвращаемое значение:
//   Структура (см. СтруктураНастроек);
//
Функция ПолучитьНастройкиПереопределяемый(знач Параметр)
	//НастройкиПоУмолчанию = СтруктураНастроек();
	//
	//// получение настроек из другого объекта хранения
	//НастройкиИзХранилища = Неопределено;
	//Если НЕ ТипЗнч(НастройкиИзХранилища) = Тип("Структура") Тогда
	//	НастройкиИзХранилища = Новый Структура;
	//КонецЕсли;
	//
	//ЗаполнитьЗначенияСвойств(НастройкиПоУмолчанию, НастройкиИзХранилища);
	//
	//Возврат НастройкиПоУмолчанию;
КонецФункции

#КонецОбласти

#КонецЕсли 