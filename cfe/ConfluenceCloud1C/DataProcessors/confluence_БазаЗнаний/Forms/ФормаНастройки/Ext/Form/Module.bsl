
#Область УправлениеФормой

&НаСервере
Процедура ИнициализацияФормы()
	
	ЗначенияНастроек = Обработки.confluence_БазаЗнаний.ПолучитьНастройкиИзХранилища();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияНастроек);
	ЗаполнитьЗначенияСвойств(Объект, ЗначенияНастроек);
	
	ЭтотОбъект.РазделПоУмолчанию = (Объект.ИдентификаторРаздела > 0);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, ИмяРеквизита)

	Если НЕ Обработано.Найти(ИмяРеквизита) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Обработано.Добавить(ИмяРеквизита);

	Элементы	= Форма.Элементы;
	Объект		= Форма.Объект;

	#Область Наборы
	
	Если ИмяРеквизита = "РеквизитыРазделПоУмолчанию" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "ИдентификаторРаздела");
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, "СкрытьПолеВводаРаздела");
	КонецЕсли;

	#КонецОбласти
	
	#Область Элементы
	
	Если ИмяРеквизита = "ИдентификаторРаздела" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"ИдентификаторРаздела", "ТолькоПросмотр", НЕ Форма.РазделПоУмолчанию);
	КонецЕсли;
	
	Если ИмяРеквизита = "СкрытьПолеВводаРаздела" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"СкрытьПолеВводаРаздела", "ТолькоПросмотр", НЕ Форма.РазделПоУмолчанию ИЛИ Объект.ИдентификаторРаздела = 0);
	КонецЕсли;

	#КонецОбласти 
	
	#Область ТабЧасть_Имя
	
	//Если ИмяРеквизита = "ИмяТабличнойЧастиОтветственный" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
	//	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	//		"ИмяТабличнойЧастиОтветственный", "ТолькоПросмотр", ЗначениеЗаполнено(Объект.Ответственный));
	//КонецЕсли;

	#КонецОбласти
	
	#Область Команды
	
	//Если ИмяРеквизита = "КомандаЗаполнить" ИЛИ ПустаяСтрока(ИмяРеквизита) Тогда
	//	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
	//		"ТаблицаФормыЗаполнить", "Видимость", НЕ Объект.Проведен);
	//КонецЕсли;

	#КонецОбласти 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьУсловноеОформление(Форма, ИменаРеквизитов = "")

	Если ТипЗнч(ИменаРеквизитов) = Тип("Строка") Тогда
		Если ПустаяСтрока(ИменаРеквизитов) Тогда
			МассивИмен = Новый Массив;
			МассивИмен.Добавить("");
		Иначе
			МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаРеквизитов, ",");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИменаРеквизитов) = Тип("Массив") Тогда
		МассивИмен = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ИменаРеквизитов);
	Иначе
		Возврат;
	КонецЕсли;
 
	//Форма.ТолькоПросмотр = (Форма.СостоянияЗаблокировано.Найти(Форма.СведенияОЗаявкеСостояние) <> Неопределено);

	Обработано = Новый Массив;
	Для Каждого ИмяРеквизита Из МассивИмен Цикл
		УстановитьУсловноеОформлениеРеквизита(Форма, Обработано, СокрЛП(ИмяРеквизита));
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресСервераПриИзменении(Элемент)
	
	ЗаполнитьСписокПространствНаКлиенте();
	
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыРазделПоУмолчанию");
	
КонецПроцедуры

&НаКлиенте
Процедура РазделПоУмолчаниюПриИзменении(Элемент)
	
	Если НЕ ЭтотОбъект.РазделПоУмолчанию Тогда
		Объект.ИдентификаторРаздела		= 0;
		Объект.СкрытьПолеВводаРаздела	= Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформление(ЭтотОбъект, "РеквизитыРазделПоУмолчанию");
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторРазделаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ИдентификаторРаздела) Тогда
		Объект.СкрытьПолеВводаРаздела = Ложь;
	КонецЕсли;
	
	ЗаполнитьСписокСтраницНаКлиенте();
	
	УстановитьУсловноеОформление(ЭтотОбъект, "СкрытьПолеВводаРаздела");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализацияФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСписокПространствНаКлиенте();
	ЗаполнитьСписокСтраницНаКлиенте();
	
	УстановитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЭтотОбъект.Модифицированность Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(ЭтотОбъект,
			Отказ,
			ЗавершениеРаботы,
			НСтр("ru='Настройки были изменены. Закрыть форму без сохранения?'") ,
			"ЗакрытьФормуБезПодтверждения",
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтотОбъект.РазделПоУмолчанию Тогда
		ПроверяемыеРеквизиты.Добавить("Объект.ИдентификаторРаздела");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если НЕ ЭтотОбъект.ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Модифицированность Тогда
		СтруктураНастроек = ЗаписатьНастройкиВХранилищеНаСервере();
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(СтруктураНастроек);	
КонецПроцедуры

#КонецОбласти

#Область ЗавершениеНемодальныхВызовов

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДопПараметры) Экспорт	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='ПередЗакрытиемЗавершение'")); 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура ЗаполнитьСписокПространствНаКлиенте()
	Если ПустаяСтрока(Объект.АдресСервера) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиПодключения	= confluence_ApiClientServer.get_connection_settings(Объект.АдресСервера);
	ПараметрыЗапроса		= confluence_ApiClientServer.get_all_spaces_params(0, 0); 
	
	РезультатЗапроса = confluence_ApiClientServer.get_all_spaces(НастройкиПодключения, ПараметрыЗапроса); 
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ИдентификаторРаздела.СписокВыбора.Количество() > 0 Тогда
		Элементы.ИдентификаторРаздела.СписокВыбора.Очистить();
	КонецЕсли;
	
	Для Каждого СтруктураРаздела Из РезультатЗапроса.Значения Цикл
		Элементы.ИдентификаторРаздела.СписокВыбора.Добавить(СтруктураРаздела.Идентификатор, СтруктураРаздела.Наименование);
	КонецЦикла;
	
	Если Элементы.ИдентификаторРаздела.СписокВыбора.НайтиПоЗначению(Объект.ИдентификаторРаздела) = Неопределено Тогда
		Объект.ИдентификаторРаздела = 0;
	Иначе
		Объект.ИдентификаторРаздела = Объект.ИдентификаторРаздела;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте 
Процедура ЗаполнитьСписокСтраницНаКлиенте()
	ЭтотОбъект.СписокСтраниц.Очистить();
	
	// get_pageid_from_space
КонецПроцедуры

&НаСервере 
Функция ЗаписатьНастройкиВХранилищеНаСервере()
	СтруктураНастроек = Обработки.confluence_БазаЗнаний.СтруктураНастроек();
	ЗаполнитьЗначенияСвойств(СтруктураНастроек, ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(СтруктураНастроек, Объект);
	
	Обработки.confluence_БазаЗнаний.ЗаписатьНастройкиВХранилище(СтруктураНастроек);
	
	ЭтотОбъект.Модифицированность = Ложь;
	
	Возврат СтруктураНастроек;
КонецФункции

#КонецОбласти
