
#Область УправлениеФормой
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторПространстваПриИзменении(Элемент)
	
	ОбновитьДеревоСтраницНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Попытка
		ТипДанныхСсылки			= ДанныеСобытия.Element.dataset.linkedResourceType;
		ИдентификаторСтраницы	= ДанныеСобытия.Element.dataset.linkedResourceId;
	Исключение
		ИдентификаторСтраницы = "";
	КонецПопытки;
	
	Если ТипДанныхСсылки <> "page" Тогда
		ИдентификаторСтраницы = "";
	КонецЕсли;
	
	ВыполнитьПереходНаСтраницу(ИдентификаторСтраницы);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ДеревоСтраниц

&НаКлиенте
Процедура ДеревоСтраницПередРазворачиванием(Элемент, Строка, Отказ)
	ТекущиеДанные = ЭтотОбъект.ДеревоСтраниц.НайтиПоИдентификатору(Строка);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ПодчиненныеЗаполнены Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПодчиненныеСтраницыДереваНаКлиенте(ТекущиеДанные, ТекущиеДанные.Идентификатор);
	ТекущиеДанные.ПодчиненныеЗаполнены = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.ДеревоСтраниц.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьПереходНаСтраницу(ТекущиеДанные.Идентификатор);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.АдресСервера = "https://progtb.atlassian.net";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьТаблицуПространстваНаКлиенте();
	ОбновитьСписокВыбораПространствНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
#КонецОбласти

#Область ЗавершениеНемодальныхВызовов
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте 
Процедура ВыполнитьПереходНаСтраницу(знач ИдентификаторСтраницы)
	Если НЕ ЗначениеЗаполнено(ИдентификаторСтраницы) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ПолеHTML = ПолучитьСодержимоеСтраницыНаКлиенте(ИдентификаторСтраницы);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицуПространстваНаКлиенте()
	ЭтотОбъект.Пространства.Очистить();
	
	НастройкиПодключения	= confluence_ApiClientServer.get_connection_settings(Объект.АдресСервера);
	ПараметрыЗапроса		= confluence_ApiClientServer.get_all_spaces_params(0, 0); 
	
	РезультатЗапроса = confluence_ApiClientServer.get_all_spaces(НастройкиПодключения, ПараметрыЗапроса); 
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураРаздела Из РезультатЗапроса.Значения Цикл
		НоваяСтрока = ЭтотОбъект.Пространства.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураРаздела);
		
		НоваяСтрока.СтраницаИдентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			СтруктураРаздела.ДомашняяСтраница, "Идентификатор");
			
		НоваяСтрока.СсылкаПросмотр = НастройкиПодключения.АдресСервераWiki + СтруктураРаздела.ДомашняяСтраница.Ссылки.Просмотр;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте 
Процедура ОбновитьСписокВыбораПространствНаКлиенте()
	Элементы.ИдентификаторПространства.СписокВыбора.Очистить();
	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект.Пространства Цикл
		Элементы.ИдентификаторПространства.СписокВыбора.Добавить(СтрокаТаблицы.Идентификатор, СтрокаТаблицы.Наименование);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте 
Процедура ОбновитьДеревоСтраницНаКлиенте()
	
	СтраницаПространства = ПолучитьИдентификаторСтраницыПространстваНаСервере();
	Если ПустаяСтрока(СтраницаПространства) Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не указано пространство'"), ,
			"ИдентификаторПространства"); 
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПодчиненныеСтраницыДереваНаКлиенте(ЭтотОбъект.ДеревоСтраниц, СтраницаПространства);
	
КонецПроцедуры

&НаКлиенте 
Процедура ЗаполнитьПодчиненныеСтраницыДереваНаКлиенте(знач ЭлементРодитель, знач ИдентификаторРодитель)
	
	ЭлементРодитель.ПолучитьЭлементы().Очистить();
	
	НастройкиПодключения	= confluence_ApiClientServer.get_connection_settings(Объект.АдресСервера);
	ПараметрыЗапроса		= confluence_ApiClientServer.get_page_child_by_type_params(0, 0);
	
	РезультатЗапроса = confluence_ApiClientServer.get_child_pages(НастройкиПодключения, ИдентификаторРодитель);
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураКонтента Из РезультатЗапроса.Значения Цикл
		НоваяСтрока = ЭлементРодитель.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураКонтента);
		
		НоваяСтрока.СсылкаПросмотр = НастройкиПодключения.АдресСервераWiki + СтруктураКонтента.Ссылки.Просмотр;
		
		// если существуют подчиненные
		Если НоваяСтрока.ЕстьПодчиненные Тогда
			НоваяСтрока.ПолучитьЭлементы().Добавить();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте 
Функция ПолучитьСодержимоеСтраницыНаКлиенте(знач ИдентификаторСтраницы)
	НастройкиПодключения = confluence_ApiClientServer.get_connection_settings(Объект.АдресСервера);
	
	СтруктураКонтента = confluence_ApiClientServer.get_page_content(НастройкиПодключения, ИдентификаторСтраницы);
	Если confluence_ApiClientServer.check_error(СтруктураКонтента, Истина) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат confluence_ApiClientServer.convert_storage_to_view(НастройкиПодключения, СтруктураКонтента); 
КонецФункции

&НаСервере 
Функция ПолучитьИдентификаторСтраницыПространстваНаСервере()
	СтруктураОтбора = Новый Структура("Идентификатор", ЭтотОбъект.ИдентификаторПространства);
	СтрокиПространства = ЭтотОбъект.Пространства.НайтиСтроки(СтруктураОтбора);
	Если СтрокиПространства.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат СтрокиПространства[0].СтраницаИдентификатор;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Раздел
#КонецОбласти

 
