
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Пространства

&НаКлиенте
Процедура ПространстваПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.Пространства.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекущиеДанные.СсылкаПросмотр) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ПолеHTML = ТекущиеДанные.СсылкаПросмотр;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.АдресСервера = "https://progtb.atlassian.net";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
 
&НаКлиенте
Процедура ОбновитьПространства(Команда)
	ЭтотОбъект.Пространства.Очистить();
	
	НастройкиПодключения	= confluence_ApiClientServer.get_connection_settings(Объект.АдресСервера);
	ПараметрыЗапроса		= confluence_ApiClientServer.get_all_spaces_params(0, 0); 
	
	РезультатЗапроса = confluence_ApiClientServer.get_all_spaces(НастройкиПодключения, ПараметрыЗапроса); 
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураРаздела Из РезультатЗапроса.Значения Цикл
		НоваяСтрока = ЭтотОбъект.Пространства.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураРаздела,, "Метки");
		Для Каждого СтруктураМетки Из СтруктураРаздела.Метки Цикл
			СтрокаМетки = НоваяСтрока.Метки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаМетки, СтруктураМетки);
		КонецЦикла;
		
		НоваяСтрока.ДомашняяСтраницаИдентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураРаздела.ДомашняяСтраница, "Идентификатор");
		НоваяСтрока.СсылкаПросмотр = НастройкиПодключения.АдресСервераWiki + СтруктураРаздела.ДомашняяСтраница.Ссылки.Просмотр;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницы(Команда)
	ЭтотОбъект.Страницы.Очистить();
	
	ТекущиеДанные = Элементы.Пространства.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ НЕ ЗначениеЗаполнено(ТекущиеДанные.Ключ) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Активизируйте строку пространства для получения страниц.'"));
		Возврат;
	КонецЕсли;
	
	НастройкиПодключения	= confluence_ApiClientServer.get_connection_settings(Объект.АдресСервера);
	ПараметрыЗапроса		= confluence_ApiClientServer.get_page_child_by_type_params(0, 0);
	
	РезультатЗапроса = confluence_ApiClientServer.get_child_pages(НастройкиПодключения,
		ТекущиеДанные.ДомашняяСтраницаИдентификатор);
		
	Если confluence_ApiClientServer.check_error(РезультатЗапроса, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтруктураКонтента Из РезультатЗапроса.Значения Цикл
		НоваяСтрока = ЭтотОбъект.Страницы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураКонтента);
		
		НоваяСтрока.ПространствоКлюч	= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураКонтента.Пространство, "Ключ", "");
		НоваяСтрока.ВерсияНомер			= ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураКонтента.Версия, "Номер", 0);
		НоваяСтрока.СсылкаПросмотр		= НастройкиПодключения.АдресСервераWiki + СтруктураКонтента.Ссылки.Просмотр;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
#КонецОбласти
